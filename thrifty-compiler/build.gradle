/*
 * Thrifty
 *
 * Copyright (c) Microsoft Corporation
 *
 * All rights reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the License);
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * THIS CODE IS PROVIDED ON AN  *AS IS* BASIS, WITHOUT WARRANTIES OR
 * CONDITIONS OF ANY KIND, EITHER EXPRESS OR IMPLIED, INCLUDING
 * WITHOUT LIMITATION ANY IMPLIED WARRANTIES OR CONDITIONS OF TITLE,
 * FITNESS FOR A PARTICULAR PURPOSE, MERCHANTABLITY OR NON-INFRINGEMENT.
 *
 * See the Apache Version 2.0 License for specific language governing permissions and limitations under the License.
 */

apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'

dependencies {
    implementation project(':thrifty-schema')
    implementation project(':thrifty-java-codegen')
    implementation project(':thrifty-kotlin-codegen')
    implementation project(':thrifty-compiler-plugins')

    implementation libraries.clikt
    implementation libraries.kotlin
}

sourceSets {
    main {
        java {
            srcDir 'src/main/java'

            // Include the generated test classes in the regular build
            srcDir 'build/generated-src/gen/java'
        }
    }
}

shadowJar {
    mergeServiceFiles {
        exclude 'META-INF/services/*'
    }

    //configurations = [project.configurations.runtime, project.configurations.compile]
}

mainClassName = 'com.microsoft.thrifty.compiler.ThriftyCompiler'

task compileTestCase(type: Exec) {
    def jarTask = project.tasks['shadowJar'] as Jar

    dependsOn jarTask

    executable 'java'
    args = ['-jar', jarTask.archivePath.absolutePath, "--use-java-style-names", "--out=$projectDir/build/generated-src/gen/java", "$projectDir/TestThrift.thrift"]
}

afterEvaluate {
    if (project.tasks.getByName('compileTestCase') != null) {
        project.tasks['compileJava'].dependsOn project.tasks.create('deleteTestCaseJava', Delete) {
            delete "$projectDir/build/generated-src/gen"
        }
    }
}