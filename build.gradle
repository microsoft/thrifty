/*
 * Thrifty
 *
 * Copyright (c) Microsoft Corporation
 *
 * All rights reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the License);
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * THIS CODE IS PROVIDED ON AN  *AS IS* BASIS, WITHOUT WARRANTIES OR
 * CONDITIONS OF ANY KIND, EITHER EXPRESS OR IMPLIED, INCLUDING
 * WITHOUT LIMITATION ANY IMPLIED WARRANTIES OR CONDITIONS OF TITLE,
 * FITNESS FOR A PARTICULAR PURPOSE, MERCHANTABLITY OR NON-INFRINGEMENT.
 *
 * See the Apache Version 2.0 License for specific language governing permissions and limitations under the License.
 */

allprojects {
    repositories {
        mavenCentral()
    }

    group GROUP
    version VERSION

    project.ext {
        libraries = [
                guava: [
                        'com.google.guava:guava:23.0'
                ],

                okio: [
                        'com.squareup.okio:okio:1.14.1'
                ],

                javaPoet: [
                        'com.squareup:javapoet:1.11.1'
                ],

                testing: [
                        'junit:junit:4.12',
                        'org.mockito:mockito-core:1.10.19',
                        'com.google.truth:truth:0.28',
                        'org.hamcrest:hamcrest-all:1.3'
                ]
        ]
    }
}

buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath 'net.ltgt.gradle:gradle-errorprone-plugin:0.0.14'
    }
}

subprojects { sp ->
    apply plugin: 'java-library'
    apply plugin: 'idea'
    apply plugin: 'checkstyle'
    apply plugin: 'net.ltgt.errorprone'
    apply plugin: 'jacoco'

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    checkstyle {
        configFile = rootProject.file("config/checkstyle/checkstyle.xml")
        configProperties = ["checkstyle.cache.file": file('build/checkstyle.cache')]
        showViolations = true
        sourceSets = [sourceSets.main]
    }

    configurations.errorprone {
        resolutionStrategy.force 'com.google.errorprone:error_prone_core:2.0.18'
    }

    tasks.withType(Checkstyle) {
        exclude '**/antlr/**'
    }

    tasks.withType(JavaCompile) {
        options.fork = true
        options.incremental = true
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        from 'build/docs/javadoc'
        classifier = 'javadoc'
    }

    task sourcesJar(type: Jar) {
        from sourceSets.main.allSource
        classifier = 'sources'
    }

    artifacts {
        archives jar
        archives javadocJar
        archives sourcesJar
    }

    test {
        testLogging {
            events "failed"
            exceptionFormat "full"
            showStackTraces true
            showExceptions true
            showCauses true
        }
    }
}

apply plugin: 'jacoco'

task codeCoverageReport(type: JacocoReport) {
    executionData fileTree(project.rootDir.absolutePath).include('**/build/jacoco/*.exec')

    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.enabled true
        xml.destination file("$buildDir/reports/jacoco/report.xml")
        html.enabled true
        csv.enabled false
    }

    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it,
            exclude: [
                    // leave out auto-generated code in thrifty-schema
                    '**/AutoValue_*',
                    '**/antlr/*',

                    // ditto for Apache-compiler-generated test classes
                    'com/microsoft/thrifty/test/gen/*',
            ])
        })
    }
}

codeCoverageReport.dependsOn {
    subprojects*.test
}

afterEvaluate {
    subprojects {
        apply from: file("${rootDir}/gradle/gradle-mvn-push.gradle")
    }
}
