/*
 * Thrifty
 *
 * Copyright (c) Microsoft Corporation
 *
 * All rights reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the License);
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * THIS CODE IS PROVIDED ON AN  *AS IS* BASIS, WITHOUT WARRANTIES OR
 * CONDITIONS OF ANY KIND, EITHER EXPRESS OR IMPLIED, INCLUDING
 * WITHOUT LIMITATION ANY IMPLIED WARRANTIES OR CONDITIONS OF TITLE,
 * FITNESS FOR A PARTICULAR PURPOSE, MERCHANTABLITY OR NON-INFRINGEMENT.
 *
 * See the Apache Version 2.0 License for specific language governing permissions and limitations under the License.
 */

plugins {
    id 'thrifty-kotlin-module' apply false
    id 'jacoco'
}

tasks.register("codeCoverageReport", JacocoReport) { t ->
    t.dependsOn subprojects*.test

    t.executionData fileTree(project.rootDir.absolutePath).include('**/build/jacoco/*.exec')

    subprojects.each {
        t.sourceSets it.sourceSets.main
    }

    t.reports {
        xml.required = true
        xml.destination layout.buildDirectory.file("reports/jacoco/report.xml").map { it.asFile }
        html.required = true
        csv.required = false
    }

    def filters = [ "**/AutoValue_*", "**/antlr/*", "com/microsoft/thrifty/test/gen/*"]
    t.classDirectories.setFrom(files(t.classDirectories.files.collect {
        fileTree(dir: it, exclude: filters)
    }))
}

wrapper {
    gradleVersion = "7.6"
    distributionType = Wrapper.DistributionType.BIN
}

def ci = providers.environmentVariable("CI")
def githubRepository = providers.environmentVariable("GITHUB_REPOSITORY")
def githubBranchName = providers.environmentVariable("GITHUB_REF_NAME")
def githubBaseRef = providers.environmentVariable("GITHUB_BASE_REF")
def versionName = providers.gradleProperty("VERSION_NAME")
def currentJavaVersion = providers.systemProperty("java.version")
def publishSnapshotProperty = providers.gradleProperty("PUBLISH_SNAPSHOT")

tasks.register("publishSnapshot") {
    def isCi = ci.getOrElse("") == "true"
    def isMainRepo = githubRepository.getOrElse("").matches("[Mm]icrosoft/thrifty")
    def isMainBranch = githubBranchName.getOrElse("") == "master"
    def isNotPR = githubBaseRef.getOrElse("") == ""
    def isSnapshot = versionName.get().endsWith("-SNAPSHOT")
    def isJava8 = currentJavaVersion.map { JavaVersion.toVersion(it) }.get().isJava8()
    def isConfiguredForSnapshots = publishSnapshotProperty.getOrElse("false") == "true"

    // We only want to upload snapshots if we're a CI build,
    // for the main repo, on the main branch (and not due to a
    // new/updated PR), and our current version is a snapshot version,
    // _and_ we're being built using JDK 8.

    if (!isCi) {
        doFirst { logger.lifecycle("Not a CI build") }
    } else if (!isMainRepo) {
        doFirst { logger.lifecycle("Wrong repo") }
    } else if (!isMainBranch) {
        doFirst { logger.lifecycle("Wrong branch") }
    } else if (!isNotPR) {
        doFirst { logger.lifecycle("Pull request") }
    } else if (!isSnapshot) {
        doFirst { logger.lifecycle("Not a snapshot") }
    } else if (!isJava8) {
        doFirst { logger.lifecycle("Wrong JDK version") }
    } else if(!isConfiguredForSnapshots) {
        doFirst { logger.lifecycle("Not configured to publish snapshots - did you forget -PPUBLISH_SNAPSHOTS=true?") }
    } else {
        // whew
        dependsOn tasks.named("publish")
    }
}
