/*
 * Thrifty
 *
 * Copyright (c) Microsoft Corporation
 *
 * All rights reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the License);
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * THIS CODE IS PROVIDED ON AN  *AS IS* BASIS, WITHOUT WARRANTIES OR
 * CONDITIONS OF ANY KIND, EITHER EXPRESS OR IMPLIED, INCLUDING
 * WITHOUT LIMITATION ANY IMPLIED WARRANTIES OR CONDITIONS OF TITLE,
 * FITNESS FOR A PARTICULAR PURPOSE, MERCHANTABLITY OR NON-INFRINGEMENT.
 *
 * See the Apache Version 2.0 License for specific language governing permissions and limitations under the License.
 */

plugins {
    id 'thrifty-kotlin-module' apply false
    id 'jacoco'
}

tasks.register("codeCoverageReport", JacocoReport) { t ->
    subprojects.each {
        t.dependsOn it.tasks.withType(Test)
        t.dependsOn it.tasks.findAll { it.name == "allTests" } // MPP why are you like this
        t.sourceSets it.sourceSets.main

        def commonMain = it.file("src/commonMain/kotlin")
        def jvmMain = it.file("src/jvmMain/kotlin")
        def isMpp = commonMain.exists() || jvmMain.exists()

        if (isMpp) {
            [commonMain, jvmMain].findAll(File::exists).each { d -> t.sourceDirectories.from(d) }
            t.classDirectories.from(it.fileTree("build/classes/kotlin/jvm/main"))
        }
    }

    t.executionData fileTree(project.rootDir.absolutePath).include('**/build/jacoco/*.exec')
    t.reports {
        xml.required = true
        xml.outputLocation = layout.buildDirectory.file("reports/jacoco/report.xml")
        html.required = true
        csv.required = false
    }

    def filters = [ "**/AutoValue_*", "**/antlr/*", "com/microsoft/thrifty/test/gen/*"]
    t.classDirectories.setFrom(files(t.classDirectories.files.collect {
        fileTree(dir: it, exclude: filters)
    }))
}

wrapper {
    gradleVersion = "8.3"
    distributionType = Wrapper.DistributionType.BIN
}
