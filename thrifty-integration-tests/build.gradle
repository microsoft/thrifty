/*
 * Thrifty
 *
 * Copyright (c) Microsoft Corporation
 *
 * All rights reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the License);
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * THIS CODE IS PROVIDED ON AN  *AS IS* BASIS, WITHOUT WARRANTIES OR
 * CONDITIONS OF ANY KIND, EITHER EXPRESS OR IMPLIED, INCLUDING
 * WITHOUT LIMITATION ANY IMPLIED WARRANTIES OR CONDITIONS OF TITLE,
 * FITNESS FOR A PARTICULAR PURPOSE, MERCHANTABLITY OR NON-INFRINGEMENT.
 *
 * See the Apache Version 2.0 License for specific language governing permissions and limitations under the License.
 */

apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'

dependencies {
    implementation project(':thrifty-compiler')

    testImplementation project(':thrifty-runtime')
    testImplementation project(':thrifty-runtime-ktx')
    testImplementation project(':thrifty-test-server')
    testImplementation libraries.guava

    testImplementation libraries.kotlin
    testImplementation libraries.testing

    testImplementation 'junit:junit:4.11'
}

uploadArchives.enabled = false

sourceSets {
    test {
        java.srcDirs += ['build/generated-src/thrifty/java', 'build/generated-src/thrifty/kotlin']
    }
}

shadowJar {
    mergeServiceFiles {
        exclude 'META-INF/services/*'
    }
}

mainClassName = 'com.microsoft.thrifty.compiler.ThriftyCompiler'

task compileTestThrift(type: Exec) {
    def jarTask = project.tasks['shadowJar'] as Jar

    inputs.file("$projectDir/ClientThriftTest.thrift")
    outputs.dir("$projectDir/build/generated-src/thrifty/java")

    dependsOn jarTask

    executable 'java'
    args = ['-jar', jarTask.archivePath.absolutePath, "--out=$projectDir/build/generated-src/thrifty/java", "$projectDir/ClientThriftTest.thrift"]
}


task kompileTestThrift(type: Exec) {
    def jarTask = project.tasks['shadowJar'] as Jar

    inputs.file("$projectDir/ClientThriftTest.thrift")
    inputs.files(jarTask.outputs)
    outputs.dir("$projectDir/build/generated-src/thrifty/kotlin")

    dependsOn jarTask

    executable 'java'
    args = [
            '-jar',
            jarTask.archivePath.absolutePath,
            "--out=$projectDir/build/generated-src/thrifty/kotlin",
            "--lang=kotlin",
            "--map-type=java.util.LinkedHashMap",
            "--set-type=java.util.LinkedHashSet",
            "--list-type=java.util.ArrayList",
            "$projectDir/ClientThriftTest.thrift"
    ]
}

compileTestKotlin {
    dependsOn compileTestThrift
    dependsOn kompileTestThrift
}

task cleanTestCode(type: Delete) {
    delete "$projectDir/build/generated-src/thrifty"
}

clean {
    dependsOn cleanTestCode
}

compileTestJava {
    dependsOn compileTestThrift
    dependsOn kompileTestThrift
    options.compilerArgs += [ '-Xep:ReferenceEquality:OFF' ]
}

tasks['javadoc'].configure {
    exclude '**/generated-src/**'
}

