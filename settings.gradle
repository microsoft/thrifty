import org.gradle.api.initialization.resolve.RepositoriesMode

pluginManagement {
    repositories {
        mavenLocal()
        mavenCentral()
        gradlePluginPortal()
    }

    plugins {
        id 'com.gradle.plugin-publish' version '0.12.0'
        id 'com.github.johnrengelman.shadow' version '6.1.0'
        id 'com.vanniktech.maven.publish' version '0.17.0'
        id 'org.jetbrains.dokka' version '1.6.10'
        id 'org.jetbrains.kotlin.jvm' version "${KOTLIN_VERSION}"
        id 'org.jetbrains.kotlin.multiplatform' version "${KOTLIN_VERSION}"
    }
}

enableFeaturePreview('VERSION_CATALOGS')

dependencyResolutionManagement {
    repositoriesMode.set(RepositoriesMode.PREFER_SETTINGS)

    repositories {
        mavenLocal()
        mavenCentral()
        gradlePluginPortal()
    }

    versionCatalogs {
        deps {
            version('kotlin', "${KOTLIN_VERSION}")
            version('okio', '3.0.0')

            version('junit', '5.7.1')
            version('kotest', '5.1.0')

            alias('antlr').to('org.antlr', 'antlr4').version('4.9.1')
            alias('apacheThrift').to('org.apache.thrift', 'libthrift').version('0.12.0')
            alias('clikt').to('com.github.ajalt.clikt', 'clikt').version('3.1.0')
            alias('guava').to('com.google.guava', 'guava').version('29.0-jre')
            alias('javaPoet').to('com.squareup', 'javapoet').version('1.13.0')
            alias('kotlin-bom').to('org.jetbrains.kotlin', 'kotlin-bom').versionRef('kotlin')
            alias('kotlin-reflect').to('org.jetbrains.kotlin', 'kotlin-reflect').versionRef('kotlin')
            alias('kotlin-stdlib').to('org.jetbrains.kotlin', 'kotlin-stdlib-jdk8').versionRef('kotlin')
            alias('kotlin-stdlibCommon').to('org.jetbrains.kotlin', 'kotlin-stdlib-common').versionRef('kotlin')
            alias('kotlinx-coroutines').to('org.jetbrains.kotlinx', 'kotlinx-coroutines-core').version('1.6.0')
            alias('kotlinPoet').to('com.squareup', 'kotlinpoet').version('1.8.0')
            alias('okio').to('com.squareup.okio', 'okio').versionRef('okio')

            bundle('kotlin', ['kotlin-stdlib', 'kotlin-reflect'])

            alias('junit').to('org.junit.jupiter', 'junit-jupiter').versionRef('junit')
            alias('junitApi').to('org.junit.jupiter', 'junit-jupiter-api').versionRef('junit')
            alias('hamcrest').to('org.hamcrest', 'hamcrest').version('2.2')
            alias('kotest-assertions-common').to('io.kotest', 'kotest-common').versionRef('kotest')
            alias('kotest-assertions-core').to('io.kotest', 'kotest-assertions-core').versionRef('kotest')
            alias('kotest-assertions-coreJvm').to('io.kotest', 'kotest-assertions-core-jvm').versionRef('kotest')
            alias('kotest-assertions-compiler').to('io.kotest.extensions', 'kotest-assertions-compiler').version('1.0.0') // They forgot to publish newer binaries
            alias('kotlin-compile-testing').to('com.github.tschuchortdev', 'kotlin-compile-testing').version('1.4.8')

            alias('kotlin-test-common').to('org.jetbrains.kotlin', 'kotlin-test-common').versionRef('kotlin')
            alias('kotlin-test-annotations-common').to('org.jetbrains.kotlin', 'kotlin-test-annotations-common').versionRef('kotlin')
            alias('kotlin-test-junit5').to('org.jetbrains.kotlin', 'kotlin-test-junit5').versionRef('kotlin')

            bundle('testing', ['junit', 'hamcrest', 'kotest-assertions-core', 'kotest-assertions-coreJvm', 'kotest-assertions-compiler', 'kotlin-compile-testing'])
        }
    }
}

rootProject.name = 'thrifty'
include 'thrifty-schema'
include 'thrifty-runtime'
include 'thrifty-java-codegen'
include 'thrifty-kotlin-codegen'
include 'thrifty-compiler'
include 'thrifty-example-postprocessor'
include 'thrifty-compiler-plugins'
include 'thrifty-test-server'
include 'thrifty-integration-tests'
include 'thrifty-gradle-plugin'
