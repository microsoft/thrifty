/*
 * Thrifty
 *
 * Copyright (c) Microsoft Corporation
 *
 * All rights reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the License);
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * THIS CODE IS PROVIDED ON AN  *AS IS* BASIS, WITHOUT WARRANTIES OR
 * CONDITIONS OF ANY KIND, EITHER EXPRESS OR IMPLIED, INCLUDING
 * WITHOUT LIMITATION ANY IMPLIED WARRANTIES OR CONDITIONS OF TITLE,
 * FITNESS FOR A PARTICULAR PURPOSE, MERCHANTABLITY OR NON-INFRINGEMENT.
 *
 * See the Apache Version 2.0 License for specific language governing permissions and limitations under the License.
 */

import org.gradle.api.attributes.java.TargetJvmVersion
import org.jetbrains.kotlin.gradle.plugin.KotlinPlatformType

plugins {
    id 'thrifty-jvm-module'
    id 'org.jetbrains.kotlin.multiplatform'
    id 'com.vanniktech.maven.publish'
}

description = 'Provides a minimal Thrift runtime to support classes generated by Thrifty'

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs = ['-Xjvm-default=enable']
    }
}

kotlin {
    jvm {
        compilations.main.kotlinOptions {
            jvmTarget = "1.8"
        }
    }

    sourceSets {
        all {
            languageSettings {
                optIn("kotlin.ExperimentalMultiplatform")
            }
        }

        commonMain {
            dependencies {
                api deps.kotlin.stdlibCommon
                api deps.okio
                api deps.kotlinx.coroutines
            }
        }

        commonTest {
            dependencies {
                implementation deps.kotlin.test.common
                implementation deps.kotlin.test.annotations.common
                implementation deps.kotest.assertions.common
                implementation deps.kotest.assertions.core
            }
        }

        jvmMain {
        }

        jvmTest {
            dependencies {
                implementation deps.kotlin.test.junit5
                implementation deps.kotest.assertions.coreJvm
                implementation deps.junit
            }
        }
    }
}

jvmTest {
    useJUnitPlatform()
}

// What have I gotten myself in to
configurations {
    jvmApiElements {
        attributes {
            attribute(KotlinPlatformType.attribute, KotlinPlatformType.jvm)
            attribute(TargetJvmVersion.TARGET_JVM_VERSION_ATTRIBUTE, 8)
            attribute(Bundling.BUNDLING_ATTRIBUTE, project.objects.named(Bundling, "external"))
        }
    }

    jvmRuntimeElements {
        attributes {
            attribute(KotlinPlatformType.attribute, KotlinPlatformType.jvm)
            attribute(TargetJvmVersion.TARGET_JVM_VERSION_ATTRIBUTE, 8)
            attribute(Bundling.BUNDLING_ATTRIBUTE, project.objects.named(Bundling, "external"))
        }
    }
}
